plugins {
    id 'groovy'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'java-gradle-plugin'
    id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
}
apply plugin: 'maven-publish'


repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    google()
}

compileGroovy {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    compileOnly 'com.android.tools.build:gradle:4.1.0'

    testImplementation gradleTestKit()
    testImplementation "junit:junit:4.13"
    testImplementation "org.jetbrains.kotlin:kotlin-test:1.4.31"
}

gradlePlugin {
    plugins {
        register("io.sentry.android.gradle") {
            id = "io.sentry.android.gradle"
            implementationClass = "io.sentry.android.gradle.SentryPlugin"
        }
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact jar
            groupId 'io.sentry'
            artifactId 'sentry-android-gradle-plugin'
            version project.version
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        debug = false
        version = "0.40.0"
        verbose = true
        android = true
        outputToConsole = true
        ignoreFailures = false
        enableExperimentalRules = true
        filter {
            exclude("**/generated/**")
            include("**/kotlin/**")
        }
    }
}

modifyPom {
    project {
        name 'Sentry Android Gradle Plugin'
        description 'Sentry Android Gradle Plugin'
        url 'https://github.com/getsentry/sentry-android-gradle-plugin'

        scm {
            url 'https://github.com/getsentry/sentry-android-gradle-plugin'
            connection 'scm:https://github.com/getsentry/sentry-android-gradle-plugin.git'
            developerConnection 'scm:git@github.com:getsentry/sentry-android-gradle-plugin.git'
        }

        licenses {
            license {
                name 'MIT'
                url 'https://github.com/getsentry/sentry-android-gradle-plugin/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'getsentry'
                name 'Sentry Team and Contributors'
                email 'oss@sentry.io'
            }
        }
    }
}
